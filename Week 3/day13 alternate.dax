EVALUATE
VAR playerFirstGame =
    GENERATE(
        SELECTCOLUMNS(
            'Players',
            "Given Name", 'Players'[Given Name],
            "Family Name", 'Players'[Family Name],
            "Birth Date", 'Players'[Birth Date]
        ),
        VAR firstGame =
            MINX(
                RELATEDTABLE( 'Players Appearences' ),
                'Players Appearences'[Match Date]
            )
        VAR age =
            IF(
                ISBLANK( [Birth Date] ) || ISBLANK( firstGame ),
                BLANK( ),
                CONVERT(
                    firstGame - CONVERT( [Birth Date], DATETIME ),
                    INTEGER
                )
            )
        VAR ageYears = INT( DIVIDE( age, 365 ) )
        VAR ageMonths = INT( DIVIDE( age - ageYears * 365, 30 ) )
        VAR ageDays = age - ageYears * 365 - ageMonths * 30
        RETURN
            ROW(
                "@First Game", firstGame,
                "@Age in Days", age,
                "@Years", ageYears,
                "@Months", ageMonths,
                "@Days", ageDays
            )
    )
RETURN
    TOPN( 1, FILTER( playerFirstGame, NOT ISBLANK( [@Age in Days] ) ), [@Age in Days] , ASC)
    
ORDER BY [@Age in Days] ASC